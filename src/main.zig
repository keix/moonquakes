const std = @import("std");
const TValue = @import("core/value.zig").TValue;
const Proto = @import("core/proto.zig").Proto;
const VM = @import("vm/vm.zig").VM;
const opcodes = @import("compiler/opcodes.zig");
const Instruction = opcodes.Instruction;
const lexer = @import("compiler/lexer.zig");
const parser = @import("compiler/parser.zig");

pub fn main() !void {
    const stdout = std.io.getStdOut().writer();
    var gpa = std.heap.GeneralPurposeAllocator(.{}){};
    defer _ = gpa.deinit();
    const allocator = gpa.allocator();

    // Get command line arguments
    const args = try std.process.argsAlloc(allocator);
    defer std.process.argsFree(allocator, args);

    var source: []const u8 = undefined;
    var should_free_source = false;

    if (args.len > 1) {
        // Read from file
        const file_path = args[1];
        try stdout.print("Reading from file: {s}\n", .{file_path});

        const file = std.fs.cwd().openFile(file_path, .{}) catch |err| switch (err) {
            error.FileNotFound => {
                try stdout.print("Error: File '{s}' not found\n", .{file_path});
                return;
            },
            else => return err,
        };
        defer file.close();

        const file_size = try file.getEndPos();
        const file_contents = try allocator.alloc(u8, file_size);
        _ = try file.readAll(file_contents);
        source = file_contents;
        should_free_source = true;
    } else {
        try stdout.print("Usage: moonquakes <lua_file>\n", .{});
        return;
    }
    defer if (should_free_source) allocator.free(source);

    try stdout.print("Testing parser with: {s}\n", .{source});
    try stdout.print("Tokens:\n", .{});
    lexer.dumpAllTokens(source);
    try stdout.print("\n", .{});

    // Test parser
    var lx = lexer.Lexer.init(source);
    var proto_builder = parser.ProtoBuilder.init(allocator);
    defer proto_builder.deinit();

    var p = parser.Parser.init(&lx, &proto_builder);
    try p.parseChunk();

    const proto = try proto_builder.toProto(allocator);
    defer allocator.free(proto.code);
    defer allocator.free(proto.k);

    try stdout.print("Generated bytecode:\n", .{});
    for (proto.code, 0..) |instr, i| {
        try stdout.print("{d}: {}\n", .{ i, instr });
    }

    try stdout.print("Constants:\n", .{});
    for (proto.k, 0..) |constant, i| {
        try stdout.print("K{d}: {}\n", .{ i, constant });
    }
    try stdout.print("\n", .{});

    // Test generated bytecode on VM
    var vm = VM.init();
    const result = try vm.execute(&proto);

    try stdout.print("Moonquakes speaks for the first time!\n", .{});
    try stdout.print("Compiling and executing Lua code: {s}\n", .{source});
    switch (result) {
        .none => try stdout.print("Result: nil\n", .{}),
        .single => |val| try stdout.print("Result: {}\n", .{val}),
        .multiple => |vals| {
            try stdout.print("Results: ", .{});
            for (vals, 0..) |val, i| {
                if (i > 0) try stdout.print(", ", .{});
                try stdout.print("{}", .{val});
            }
            try stdout.print("\n", .{});
        },
    }
}
