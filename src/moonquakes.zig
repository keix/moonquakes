const std = @import("std");
const TValue = @import("core/value.zig").TValue;
const Proto = @import("core/proto.zig").Proto;
const VM = @import("vm/vm.zig").VM;
const lexer = @import("compiler/lexer.zig");
const parser = @import("compiler/parser.zig");

/// This project is a clean-room implementation inspired by
/// the Lua 5.4 language specification.
/// It does not include or depend on the original Lua source code.
///
/// Use it freely. Embed it freely. Learn from it freely.
///
/// Copyright (c) 2025 KEI SAWAMURA. Licensed under the MIT License.
///
pub const Moonquakes = struct {
    allocator: std.mem.Allocator,

    pub fn init(allocator: std.mem.Allocator) Moonquakes {
        return .{ .allocator = allocator };
    }

    pub fn deinit(self: *Moonquakes) void {
        _ = self;
    }

    /// Compile Lua source code into bytecode
    pub fn compile(self: *Moonquakes, source: []const u8) !Proto {
        var lx = lexer.Lexer.init(source);
        var builder = parser.ProtoBuilder.init(self.allocator);
        defer builder.deinit();

        var p = parser.Parser.init(&lx, &builder);
        try p.parseChunk();

        return try builder.toProto(self.allocator);
    }

    /// Execute compiled bytecode
    pub fn run(self: *Moonquakes, proto: *const Proto) !VM.ReturnValue {
        var vm = try VM.init(self.allocator);
        defer vm.deinit();
        return try vm.execute(proto);
    }

    /// Compile and execute Lua source in one step
    pub fn runSource(self: *Moonquakes, source: []const u8) !VM.ReturnValue {
        const proto = try self.compile(source);
        defer self.allocator.free(proto.code);
        defer self.allocator.free(proto.k);

        return try self.run(&proto);
    }

    /// Load and execute Lua file
    pub fn loadFile(self: *Moonquakes, file_path: []const u8) !VM.ReturnValue {
        const file = try std.fs.cwd().openFile(file_path, .{});
        defer file.close();

        const file_size = try file.getEndPos();
        const source = try self.allocator.alloc(u8, file_size);
        defer self.allocator.free(source);

        _ = try file.readAll(source);
        return try self.runSource(source);
    }

    /// Debug: dump all tokens from source
    pub fn dumpTokens(_: *Moonquakes, source: []const u8) void {
        lexer.dumpAllTokens(source);
    }

    /// Debug: dump bytecode instructions
    pub fn dumpProto(_: *Moonquakes, proto: *const Proto) !void {
        const stdout = std.io.getStdOut().writer();

        try stdout.print("Generated bytecode:\n", .{});
        for (proto.code, 0..) |instr, i| {
            try stdout.print("{d}: {}\n", .{ i, instr });
        }

        try stdout.print("Constants:\n", .{});
        for (proto.k, 0..) |constant, i| {
            try stdout.print("K{d}: {}\n", .{ i, constant });
        }
    }

    /// Debug: comprehensive analysis of source code
    pub fn analyze(self: *Moonquakes, source: []const u8) !void {
        const stdout = std.io.getStdOut().writer();

        try stdout.print("=== Moonquakes Analysis ===\n", .{});
        try stdout.print("Source: {s}\n\n", .{source});

        try stdout.print("Tokens:\n", .{});
        self.dumpTokens(source);
        try stdout.print("\n", .{});

        const proto = try self.compile(source);
        defer self.allocator.free(proto.code);
        defer self.allocator.free(proto.k);

        try self.dumpProto(&proto);
        try stdout.print("\n", .{});

        const result = try self.run(&proto);
        try stdout.print("Execution result: ", .{});
        switch (result) {
            .none => try stdout.print("nil\n", .{}),
            .single => |val| try stdout.print("{}\n", .{val}),
            .multiple => |vals| {
                for (vals, 0..) |val, i| {
                    if (i > 0) try stdout.print(", ", .{});
                    try stdout.print("{}", .{val});
                }
                try stdout.print("\n", .{});
            },
        }
    }
};
